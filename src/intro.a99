*********************************************************************
*
* Show intro
*
intro:
       .proc
*      Init variables
       clr  @level
       clr  @scroll_y
       clr  @scroll_enabled
       bl   @init_area_required
       clr  @time
       clr  @displayed_time
intro_1:
       li   r0,3
       mov  r0,@lives
       clr  @trail_pointer
       clr  @fuse_trail_pointer
       clr  @fuse_active
       clr  @inactivity_time
       clr  @time_out
*      Init tix
       li   r0,init_tix_intro_data
       bl   @init_tix
       clr  @tix1_tail_pointer
*      Sparks
       li   r0,intro_init_sparks_data
       bl   @init_sparks
       clr  @n_sparks
*      Map
       li   r3,20
       bl   @init_map_with_height
*      Plot out of view parking area
       li   r0,4
       li   r1,39*8+4
       li   r2,white*256
       bl   @plot
*      Refresh
       bl   @refresh_screen
*      Screen data pointer
       li   r0,intro_screen_data
       mov  r0,@intro_screen_ptr
*      Created by
       bl   @intro_screen
*      Your marker
       li   r0,15*8
       mov  r0,@x
       li   r1,19*8
       mov  r1,@y
       bl   @join_effect
       bl   @intro_screen
*      Slow area
       bl   @intro_screen
*      Fast area
       bl   @intro_screen
*      Scores
       bl   @intro_screen
*      Bonus
       bl   @intro_screen
*      Your opponents
       li   r0,tix_tail
       mov  r0,@tix1_tail_pointer
       bl   @intro_screen
*      The Tix
       bl   @intro_screen
*      Sparx
       inct @n_sparks
       bl   @intro_screen
*      Fuse
       bl   @intro_screen
*      Spiral death trap
       bl   @intro_screen
*      Loop
       clr  @score_lsw
       clr  @score_msw
       clr  @area
       jmp  intro_1
intro_2:
       .endproc
init_tix_intro_data:
       data 1
       data 172,48,>0100,>1100
*// intro

*********************************************************************
*
* Intro screen
*
intro_screen:
       .proc
       mov  @intro_screen_ptr,r1
       mov  *r1+,@intro_texts_addr
       mov  *r1+,r0
       mov  r1,@intro_screen_ptr
       bl   @play_path
       mov  r0,r0
       jeq  intro_screen_1
       .pop  r11
       jmp    intro_2
intro_screen_1:
       .endproc
intro_screen_data:
       data texts_intro_1,intro_path_1
       data texts_intro_2,intro_path_2
       data texts_intro_3,intro_path_3
       data texts_intro_4,intro_path_4
       data texts_intro_5,intro_path_1
       data texts_intro_6,intro_path_5
       data texts_intro_7,intro_path_5
       data texts_intro_8,intro_path_6
       data texts_intro_9,intro_path_7
       data texts_intro_10,intro_path_8
       data texts_intro_11,intro_path_9
*// intro_screen

*********************************************************************
*
* Play path
*
* r0: Path to play
*
play_path:
       .proc
       mov  r0,r13
       mov  *r13+,r0
       sla  r0,3
       ai   r0,4
       mov  r0,@x
       mov  *r13+,r1
       sla  r1,3
       ai   r1,4
       mov  r1,@y
       mov  *r13+,r14
       clr  @status
       bl   @intro_game_loop
       li   r0,JOY_FI
       bl   @check_key
       jne  play_path_1
       clr  r0
       jmp  play_path_2
play_path_1:
       seto r0
play_path_2:
       .endproc
*// play_path

*********************************************************************
*
* Intro game loop
*
intro_game_loop:
       .proc
intro_game_loop_1:
       bl   @vsync
       bl   @set_vdp_regs
       bl   @check_alignment
       bl   @auto_user_input
       bl   @move_player
       bl   @plot_trail
       mov  @tix1_tail_pointer,r0
       jeq  intro_game_loop_3
       bl   @move_tix
intro_game_loop_3:
       mov  @n_sparks,r0
       jeq  intro_game_loop_4
       bl   @update_sparks
intro_game_loop_4:
       bl   @update_fuse
       bl   @upload_sprites
       bl   @upload_map
       mov  @intro_texts_addr,r0
       jeq  intro_game_loop_2
       bl   @display_overlay_texts
intro_game_loop_2:
       bl   @snd_player
       inc  @time
       mov  @status,r0
       jeq  intro_game_loop_1
       jlt  intro_game_loop_1
       .endproc
*// intro_game_loop

*********************************************************************
*
* Automatic user input (for intro)
*
auto_user_input:
       .proc
       li   r0,JOY_FI
       bl   @check_key
       jne  auto_user_input_4
       mov  @char_aligned,r0
       jeq  auto_user_input_5
       mov  *r13+,r2                   ; dx
       mov  *r13+,r3                   ; dy
       mov  *r13,r4                    ; Fire
       jeq  auto_user_input_0
       clr  @slow
auto_user_input_0:
       ai   r13,-4
       mov  r2,r0
       jne  auto_user_input_1
       mov  r3,r0
       jeq  auto_user_input_2
auto_user_input_1:
       mov  @dx,@dx_old
       mov  @dy,@dy_old
auto_user_input_2:
       mov  r2,@dx
       mov  r3,@dy
       abs  r4
       jeq  auto_user_input_3
       sla  r2,1
       sla  r3,1
auto_user_input_3:
       mov  r2,@vx
       mov  r3,@vy
*      Update auto counter
       dec  r14
       jne  auto_user_input_5
       ai   r13,6
       mov  *r13+,r14                  ; New counter
       jgt  auto_user_input_5
auto_user_input_4:
       clr  @status
       inc  @status
auto_user_input_5:
       .endproc
*// auto_user_input

*********************************************************************
*
* Display overlay texts
*
* r0: Points to list of strings to display (list of offset, string pointer)
* terminated by -1.
*
display_overlay_texts:
       .proc
       mov  r0,r5
display_overlay_texts_1:
       mov  *r5+,r0
       jlt  display_overlay_texts_2
       mov  *r5+,r1
       bl   @display_overlay_text
       jmp  display_overlay_texts_1
display_overlay_texts_2:
       .endproc
*// display_overlay_texts

*********************************************************************
*
* Display text on top of the map
*
* r0: Screen offset
* r1: String to display (0 terminated)
*
display_overlay_text:
       .proc
       mov  @name_table_flag,r2
       andi r2,>0001
       sla  r2,10
       a    r2,r0
       ai   r0,namet1
       bl   @vwad
display_overlay_text_1:
       movb *r1+,r2
       jeq  display_overlay_text_2
       ai   r2,(chr_space-32)*256
       movb r2,*r15
       jmp  display_overlay_text_1
display_overlay_text_2:
       .endproc
*// display_overlay_text

*********************************************************************
*
* Data
*
intro_screen_ptr:
       data 0
intro_texts_addr:
       data 0

intro_init_sparks_data:
       data 12,4,1,0
       data 12,map_width_pixels-4,1,0
       data 0,0,0,0
       data 0,0,0,0

txt_created_2019:
       text "CREATED 2019 BY"
       byte 0
txt_rasmus_moustgaard:
       text "RASMUS MOUSTGAARD"
       byte 0
txt_your_marker:
       text "YOUR MARKER"
       byte 0
txt_controlled_with:
       text "CONTROLLED WITH"
       byte 0
txt_joystick:
       text "JOYSTICK"
       byte 0
txt_claim_areas:
       text "CLAIM AREAS"
       byte 0
txt_by_joining_walls:
       text "BY JOINING WALLS"
       byte 0
txt_with_stix:
       text "WITH STIX"
       byte 0
txt_press_fire:
       text "PRESS FIRE"
       byte 0
txt_to_move_faster:
       text "TO MOVE FASTER"
       byte 0
txt_scores_based_on_area:
       text "SCORES BASED ON AREA"
       byte 0
txt_slow_score:
       text "SLOW SCORE"
       byte 0
txt_slow_score_number:
       text "84"
       byte 0
txt_fast_score:
       text "FAST SCORE"
       byte 0
txt_fast_score_number:
       text "42"
       byte 0
txt_claim_more_than:
       text "CLAIM MORE THAN"
       byte 0
txt_75_pct_of_playfield:
       text "75% OF PLAYFIELD"
       byte 0
txt_for_special_bonus:
       text "FOR SPECIAL BONUS"
       byte 0
txt_your_opponents:
       text "YOUR OPPONENTS:"
       byte 0
txt_the_tix:
       text "THE TIX"
       byte 0
txt_sparx:
       text "SPARX"
       byte 0
txt_the:
       text "THE"
       byte 0
txt_fuse:
       text "FUSE"
       byte 0
txt_spiral:
       text "SPIRAL"
       byte 0
txt_death:
       text "DEATH"
       byte 0
txt_trap:
       text "TRAP"
       byte 0

texts_intro_1:
       data 10*32+8,txt_created_2019
       data 11*32+7,txt_rasmus_moustgaard
       data -1
texts_intro_2:
       data 15*32+10,txt_your_marker
       data 17*32+8,txt_controlled_with
       data 18*32+11,txt_joystick
       data -1
texts_intro_3:
       data 10*32+10,txt_claim_areas
       data 11*32+7,txt_by_joining_walls
       data 12*32+11,txt_with_stix
       data -1
texts_intro_4:
       data 10*32+11,txt_press_fire
       data 11*32+9,txt_to_move_faster
       data -1
texts_intro_5:
       data 13*32+3,txt_slow_score
       data 14*32+7,txt_slow_score_number
       data 13*32+19,txt_fast_score
       data 14*32+23,txt_fast_score_number
       data -1
texts_intro_6:
       data 10*32+8,txt_claim_more_than
       data 11*32+8,txt_75_pct_of_playfield
       data 12*32+7,txt_for_special_bonus
       data -1
texts_intro_7:
       data 5*32+2,txt_your_opponents
       data -1
texts_intro_8:
       data 5*32+2,txt_your_opponents
       data 7*32+4,txt_the_tix
       data 8*32+8,txt_tix_line
       data -1
texts_intro_9:
       data 5*32+2,txt_your_opponents
       data 7*32+4,txt_the_tix
       data 8*32+8,txt_tix_line
       data 7*32+23,txt_sparx
       data -1
texts_intro_10:
       data 5*32+2,txt_your_opponents
       data 7*32+4,txt_the_tix
       data 8*32+8,txt_tix_line
       data 7*32+23,txt_sparx
       data 16*32+14,txt_the
       data 17*32+14,txt_fuse
       data -1
texts_intro_11:
       data 8*32+23,txt_spiral
       data 9*32+23,txt_death
       data 10*32+23,txt_trap
       data -1

*      start_x, start_y
*      count,dx,dy,fire
intro_path_1:
       data 0,39
       data 120,0,0,0
       data -1
intro_path_2:
       data 15,19
       data 120,0,0,0
       data -1
intro_path_3:
       data 15,19
       data 4,-1,0,0
       data 6,0,-1,0
       data 6,-1,0,0
       data 6,0,1,0
       data 16,1,0,0
       data -1
intro_path_4:
       data 20,19
       data 6,0,-1,1
       data 6,1,0,1
       data 6,0,1,1
       data 6,1,0,0
       data -1
intro_path_5:
       data 31,19
       data 120,0,0,0
       data -1
intro_path_6:
       data 31,19
       data 5,-1,0,0
       data 7,0,-1,0
       data -1
intro_path_7:
       data 26,13
       data 2,0,-1,0
       data 14,-1,0,0
       data -1
intro_path_8:
       data 13,11
       data 120,0,0,0
       data 6,0,-1,0
       data 8,1,0,0
       data 4,0,1,0
       data 6,-1,0,0
       data 2,0,-1,0
       data 5,1,0,0
       data -1
intro_path_9:
       data 19,7
       data 280,0,0,0
       data -1
